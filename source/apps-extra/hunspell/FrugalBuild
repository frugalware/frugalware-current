# Compiling time: 0.10 SBU
# Maintainer: CSÉCSY László <boobaa@frugalware.org>

pkgname=hunspell
pkgver=1.2.1
pkgrel=3
pkgdesc="An interactive Hungarian spell checker"
url="http://hunspell.sourceforge.net/"
rodepends=('hunspell-dict')
depends=('libstdc++' 'readline' 'ncurses')
makedepends=('hd2u')
groups=('apps-extra')
archs=('i686' 'x86_64')
Finclude sourceforge
up2date="lynx -dump http://hunspell.sourceforge.net/ | grep -m1 'http.*tar\.gz' | sed 's/.*l-\(.*\)\.tar.*/\1/;s/-/_/'"
source=(${source[@]} \
	http://forditas.fsf.hu/scripts/huspell \
	http://forditas.fsf.hu/scripts/huspell-po)
sha1sums=('610c44fc03570cfdb01d6f92943e8a9ae3b60c72' \
          '8beb23bebd6901d64901190f18ce996d5a9e6d4b' \
          '69ccc901cb15fbc3d02ce752d74037e1b48ce077')

[ "$CARCH" == "x86_64" ] && CXXFLAGS="$CXXFLAGS -fPIC"

build() {
	myver=${pkgver//_*/}
	Fcd $pkgname-$myver
	Fbuild --with-ui --with-readline
	Fsed "-d hu_HU_u8 " "" $Fsrcdir/huspell
	Finstall 755 huspell{,-po} /usr/bin/
	Fmv usr/bin/analyze usr/bin/analyze-hunspell
	Fsed "-1.2" "" $Fdestdir/usr/lib/pkgconfig/hunspell.pc
}

# optimization OK
