# Compiling Time: 0.01 SBU
# Maintainer: crazy <crazy@frugalware.org>

pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=7
pkgdesc="XML stylesheets for Docbook-xml transformations."
depends=('docbook-xml' 'libxslt')
rodepends=('sed')
groups=('apps')
archs=("x86_64")
_F_github_author="docbook"
_F_github_name="xslt10-stylesheets"
_F_github_tag=y
Finclude github
up2date=$pkgver
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F${pkgver}/docbook-xsl-${pkgver}.tar.bz2" \
	"https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F${pkgver}/docbook-xsl-nons-${pkgver}.tar.bz2" \
	"765567_non-recursive_string_subst.patch")
sha1sums=('42c66d90d5febfccc192b0726bcf1554af6eb792' \
          'fb4f60b3968777b5a794d1e1f9e5867b04517425' \
          'f63b4a32b7430cfdcd5be5f8968a4655092e75de')
options+=('scriptlet')

build() {

	for ns in -nons ''; do
		local installdir="/usr/share/xml/docbook/xsl-stylesheets-${pkgver}${ns}"
		Fexec cd "$Fsrcdir/$pkgname$ns-$pkgver"

		Fpatchall

		Fmkdir "$installdir"
		for fn in VERSION.xsl assembly common eclipse epub epub3 fo highlighting \
				html htmlhelp javahelp lib manpages params profiling roundtrip \
				template website xhtml xhtml-1_1 xhtml5
		do
			Fcprel $fn "$installdir"
		done
		Fln "/usr/share/xml/docbook/xsl-stylesheets-${pkgver}${ns}" usr/share/xml/docbook/xsl-stylesheets${ns}
		Fmkdir etc/xml
	done

}


# optimization OK
