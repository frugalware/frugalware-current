# Compiling Time: 4.26 SBU
# Maintainer: Christian Hamar alias krix <krics@linuxforum.hu>

pkgname=boost
pkgver=1.33.1
pkgrel=2
pkgdesc="Boost Libraries for C++"
_F_sourceforge_ext=".tar.bz2"
Finclude sourceforge
url="http://boost.org/"
depends=('libstdc++' 'python')
makedepends=('boost-jam')
groups=('devel-extra')
archs=('i686' 'x86_64')
up2date="lynx -dump 'http://sourceforge.net/project/showfiles.php?group_id=7586' | grep -v beta|grep -om1 'boost\(.*\)tar.bz2'|sed 's/.*t_\(.*\).t.*/\1/;s/_/./g'"
source=(${source/-$pkgver/_${pkgver//./_}})
sha1sums=('22682f75ff0d1ea548688e78c8cca83d599ca576')

build() {
	Fcd ${pkgname}_`echo $pkgver | sed -e 's/\./_/' | sed -e 's/\./_/'`
	Fpatchall
	
	BOOST_TOOLSET="gcc"
	TOOLSET_NAME="gcc"
	SOSUFFIX="so"
	BOOSTJAM=/usr/bin/bjam
	BUILD="release <runtime-link>dynamic"
	BUILD="${BUILD} <threading>multi"
	PYTHON_VERSION=`python -V 2>&1 |sed 's,.* \([0-9]\.[0-9]\)\(\.[0-9]\)\?.*,\1,'`

	${BOOSTJAM} -sBOOST_ROOT="$Fsrcdir" \
	    -sPYTHON_ROOT=/usr \
	    -sPYTHON_VERSION=${PYTHON_VERSION} \
	    -sTOOLS=${BOOST_TOOLSET} \
	    -sBUILD="${BUILD}" \
	    --prefix=$Fdestdir/usr \
	    --layout=system

	${BOOSTJAM} -sBOOST_ROOT="$Fsrcdir" \
	    -sPYTHON_ROOT=/usr \
	    -sPYTHON_VERSION=${PYTHON_VERSION} \
	    -sTOOLS=${BOOST_TOOLSET} \
	    -sBUILD="${BUILD}" \
	    --prefix=$Fdestdir/usr \
	    --layout=system install || return 1
	
	cd $Fsrcdir/${pkgname}_`echo $pkgver | sed -e 's/\./_/' | sed -e 's/\./_/'`/tools/build/
	Fmkdir usr/share/boost
	cp -a b* index.html v1/ v2/ $Fdestdir/usr/share/boost/

        cd $Fdestdir/usr/lib

### Braindead thing ....

	for fn in `ls -1 *.${SOSUFFIX}| cut -d- -f1 | sed 's/\.so//g' | sort | uniq`; do
		Fln "$fn-mt.${SOSUFFIX}" "/usr/lib/$fn-gcc.${SOSUFFIX}"
	done

	for fn in `ls -1 *.a| cut -d- -f1 | sed 's/\.a//g' | sort | uniq`; do
		Fln "$fn-mt.a" "/usr/lib/$fn-gcc.a"
	done
}
