# Compiling Time: 3.94 SBU
# Maintainer: DeX77 <dex77@frugalware.org>

pkgname=nodejs
pkgver=21.1.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
archs=('x86_64')
url='https://nodejs.org/'
license=('MIT')
groups=('devel-extra')
depends=('zlib' 'c-ares' 'nghttp2' 'openssl>=3.1.0' 'icu4c>=73.1' 'python3-setuptools')
makedepends=('ca-certificates' 'mold')
source=(http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz \
	ares_compat.patch \
	python312.patch )
sha1sums=('6b12b9ac87d63741e57887cdc8faf7674032841f' \
          'e9ebef1ff71573861daf3b2a8b050599d275907d' \
          'e58055f431c123d23ef2f101dfc8e95e198703c6')
_F_archive_name="node"
Fpkgversep="-v"
_F_archive_grepv="v20\|v19"
up2date="Flastverdir $url/dist/ | sed 's/v//g'"
Fconfopts="	--prefix=/usr \
		--with-intl=system-icu \
		--shared-zlib \
		--shared-cares \
		--shared-openssl \
		--openssl-use-def-ca-store \
		--enable-lto \
    		--shared-nghttp2"
_F_conf_notry="build"

options=('static' 'nolto')

build() {
	export GYP_DEFINES="linux_use_gold_flags=0 linux_use_bundled_binutils=0 linux_use_bundled_gold=0"
	# use mold

	export LDFLAGS="-fuse-ld=mold $LDFLAGS"
	Fbuild
}

# optimization OK
