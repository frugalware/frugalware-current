# Compiling Time: 3.54 SBU
# Maintainer: Baste <baste@frugalware.org>
# Contributor: bouleetbil <bouleetbil@frogdev.info>

options+=('asneeded')

pkgname=brasero
pkgver=3.11.3
pkgrel=1
pkgdesc="A CD/DVD Burning application for the GNOME desktop."
url="http://www.gnome.org/projects/brasero/"
depends=('gst1-plugins-base' 'gtk+3' 'libnotify' 'libcanberra-gtk' 'libtracker' \
	'totem-pl-parser' 'libisofs>=1.2.4' 'libburn>=1.2.4' 'libdvdcss')
makedepends=('intltool' 'gobject-introspection' 'itstool' 'gnome-common')
rodepends=('cdrdao' 'cdrkit' 'dvd+rw-tools')
groups=('gnome')
conflicts=('bonfire')
replaces=('bonfire')
archs=('i686' 'x86_64')
sha1sums=('8b7a10daed32cc0aec473eb6ba1d20ad2f10dfb8')

subpkgs=("nautilus-brasero")
subdescs=('An easy to use CD/DVD burning package for Nautilus')
subdepends=("nautilus>=3.8.0")
subrodepends=("$pkgname=$pkgver")
subgroups=('gnome')
subarchs=('i686 x86_64')
subreplaces=('nautilus-cd-burner')
subprovides=('nautilus-cd-burner')
_F_gnome_mime="y"
_F_gnome_doc="y"
_F_gnome_glib="y"
_F_gnome_desktop="y"
_F_gnome_iconcache="y"
_F_gnome_devel="y"
_F_gnome_ext=".tar.xz"
Finclude gnome gnome-scriptlet

Fconfopts+=" --enable-search --enable-playlist --enable-preview --enable-inotify --enable-libnotify --disable-schemas-install \
	--enable-caches=no --enable-introspection=yes "

build() {
	#For bug with tracker-0.16
	Fsed "0.14" "0.16" configure*
	Fbuild
	Fbuild_gnome_scriptlet
	Fsplit nautilus-brasero usr/lib/nautilus
	_F_gnome_split_doc
}

# optimization OK

