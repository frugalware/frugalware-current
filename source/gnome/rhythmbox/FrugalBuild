# Compiling time: 1.40 SBU
# Maintainer: bouleetbil <bouleetbil@frogdev.info>
# Contributor: AlexExtreme <alex@alex-smith.me.uk>

pkgname=rhythmbox
pkgver=0.13.3.3
pkgrel=1
pkgdesc="A music management application for GNOME"
url="http://www.rhythmbox.org/"
depends=('nautilus-brasero' 'rarian' 'desktop-file-utils' \
	'totem>=3.0.1-2' 'gst-plugins-base-oil' 'avahi-glib' 'libnotify' 'libsexy' 'tdb'\
	'libsoup-gnome>=2.32.0' 'dbus-glib>=0.74' 'python>=2.7' 'brasero' 'icu4c>=4.8' \
	'evolution-data-server>=3.2.0' 'gnutls>=2.12.12' 'libgudev' 'libdmapsharing' 'webkit-gtk3' 'json-glib' 'libmusicbrainz3')
makedepends=('intltool' 'gnome-doc-utils' 'gst-python' 'libgpod>=0.6.0' 'vala' 'gtk-doc' 'gnome-common' 'krb5')
rodepends=('notification-daemon')
replaces=('rhythmbox-desktop-art')
groups=('gnome')
archs=('i686' 'x86_64')
_F_gnome_glib="y"
_F_gnome_scrollkeeper="y"
_F_gnome_desktop="y"
_F_gnome_iconcache="y"
_F_gnome_devel="y"
_F_gnome_git="n"
Finclude gnome-scriptlet gnome
source=(http://ftp.frugalware.org/pub/other/people/baste/snapshots/$pkgname/$pkgname-$pkgver.tar.bz2)
unset MAKEFLAGS

Fconfopts="$Fconfopts --enable-mdns=avahi --with-playback=gstreamer-0-10 \
	--enable-daap --enable-audioscrobbler --enable-python --without-hal \
	--disable-vala --with-brasero"
#vala support is broken
sha1sums=('b49ac5d456753b39ded6e4236132b12df9ec2810')
if [ "$_F_gnome_git" != "n" ]; then
        unset sha1sums
fi
subpkgs=("rhythmbox-plugins")
subdescs=('Plugins for rhythmbox')
subdepends=("gst-python libgpod>=0.6.0 gvfs-apple libmtp>=1.1.1")
subrodepends=('rhythmbox')
subgroups=('gnome-extra')
subarchs=('i686 x86_64')

build() {
	if [ "$_F_gnome_git" != "n" ]; then
		Funpack_scm
		Fcd $pkgname
		sh ./autogen.sh
	else
		Fcd
		sh ./autogen.sh
	fi
	Fsed "-Werror" "" configure
	Fbuild
	Fsplit $pkgname-plugins usr/lib/rhythmbox/plugins
	Fbuild_gnome_scriptlet
}

# optimization OK
