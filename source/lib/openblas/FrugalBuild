# Compiling Time: 0.01 SBU
# Maintainer: DeX77 <dex+fw@dragonslave.de>

pkgname=openblas
pkgver=0.3.28
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
archs=('x86_64')
groups=('lib')
url="https://www.openblas.net/"
depends=('libgomp' 'libgfortran')
makedepends=('perl' 'gcc-gfortran')
_F_github_author=OpenMathLib
_F_github_name=OpenBLAS
_F_github_tag_v=y
_F_cmake_confopts="	-DBUILD_SHARED_LIBS=ON \
			-DBUILD_TESTING=OFF \
			-DNO_AFFINITY=ON \
			-DUSE_OPENMP=1 \
			-DNO_WARMUP=1 \
			-DCORE=CORE2 \
			-DNUM_THREADS=64 \
			-DDYNAMIC_ARCH=ON"
Finclude github cmake
sha1sums=('f75172274a7ce69901605b8c759e2e0891c9b7f4')
_F_cd_path="OpenBLAS-$pkgver"
provides=('libblas' 'libcblas' 'lapack' 'liblapacke')
conflics=('libblas' 'libcblas' 'lapack' 'liblapacke')

subpkgs=("${pkgname}64")
subdescs=("$pkgdesc 64-bit")
subarchs=('x86_64')
subgroups=('lib-extra')
subdepends=('libgfortran')
subprovides=('libblas64 libcblas64 lapack64 liblapacke64')
subconflicts=('libblas64 libcblas64 lapack64 liblapacke64')

build() {
	_F_cmake_build_dir=frugalware_build64
	CMake_build -DINTERFACE64=1
	for _lib in blas64 cblas64 lapack64 lapacke64; do
		Fln libopenblas_64.so.${pkgver%.*} usr/lib/lib${_lib}.so
		Fln libopenblas_64.so.${pkgver%.*} usr/lib/lib${_lib}.so.3
		Fln openblas64.pc usr/lib/pkgconfig/${_lib}.pc
	done

	Fsplit "${pkgname}64" \*

	Fexec cd $Fsrcdir
	_F_cmake_build_dir=frugalware_build
	CMake_build

	for _lib in blas cblas lapack lapacke; do
		Fln libopenblas.so.${pkgver%.*} usr/lib/lib${_lib}.so
		Fln libopenblas.so.${pkgver%.*} usr/lib/lib${_lib}.so.3
		Fln openblas.pc usr/lib/pkgconfig/${_lib}.pc
  done

}


# optimization OK
