# Compiling time: 2.56 SBU
# Maintainer: PacMiam <PacMiam@gmx.fr>
# Contributor: Bence Nagy <nagybence@tipogral.hu>

pkgname=claws-mail
pkgver=3.9.3
pkgrel=1
pkgdesc="The extended version of Sylpheed, a GTK+ based, lightweight, and fast e-mail client"
groups=('xapps-extra')
archs=('i686' 'x86_64')
options=('scriptlet')
depends=('libxml2>=2.7.8' 'openssl>=1.0.0' 'aspell' 'pilot-link>=0.12.1'  \
	'gtk+2>=2.20.0-2' 'gpgme' 'startup-notification' 'libldap' \
	'spamassassin' 'curl' 'libetpan>=1.1' 'gnutls>=2.12.12' \
	'hicolor-icon-theme' 'glib2>=2.18.4-1' 'db>=4.7.25' 'enchant>=1.6.0')
makedepends=('openldap>=2.4.22-2' 'enchant')
_F_sourceforge_realname="Claws Mail"
_F_sourceforge_ext=".tar.bz2"
_F_sourceforge_dirname="sylpheed-claws"
conflicts=('sylpheed-claws' 'claws-mail-plugin-smime')
replaces=('sylpheed-claws' 'claws-mail-plugin-smime' 'claws-webkit-plugin' 'claws-mail-plugins-maildir')
provides=('sylpheed-claws' 'claws-mail-plugin-smime')
Finclude sourceforge
url="http://www.claws-mail.org/"

build() {
	Fpatchall
	Fmake \
		--enable-aspell --enable-ldap \
		--enable-enchant \
		--enable-spamassassin-plugin
	cd tools || Fdie
	make || Fdie
	for tool in *.pl *.py *.sh tb2* update-po uudec; do
    		Fexerel /usr/lib/claws-mail/tools/${tool}
	done
	Ffilerel /usr/lib/claws-mail/tools/multiwebsearch.conf
	cd .. || Fdie
	Ffilerel claws-mail-64x64.png /usr/share/pixmaps/claws-mail.png
	Fmakeinstall
	Ffilerel claws-mail.png /usr/share/icons/hicolor/48x48/apps/claws-mail.png
	Ffilerel claws-mail.desktop /usr/share/applications/claws-mail.desktop
}

sha1sums=('82026ac7dfc0123544def8ef338a4d0746671284')

# optimization OK
