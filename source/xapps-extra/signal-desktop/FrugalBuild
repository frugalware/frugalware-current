# Compiling Time: 13.15 SBU
# Maintainer: DeX77 <dex+fw@dragonslave.de>

pkgname=signal-desktop
pkgver=7.59.0
pkgrel=1
pkgdesc="Signal Private Messenger for Linux"
archs=('x86_64')
groups=('xapps-extra')
url="https://signal.org"
depends=('gtk+3' 'libvips' 'libxscrnsaver' 'hicolor-icon-theme')
makedepends=('nodejs' 'git-lfs' 'x11-protos' 'kernel-headers' 'libxcrypt-compat' 'node-gyp' 'pnpm')
_F_github_author=signalapp
_F_github_tag_prefix=v
Finclude github
source=("https://github.com/signalapp/Signal-Desktop/archive/refs/tags/v$pkgver.tar.gz" \
	signal-desktop.desktop)
sha1sums=('606e60e85cca06eb71f391d31c44f420a8c8815e' \
          'f5e7541b5141b23caf4382679e364dc8c7e9a9fb')
_F_cd_path="Signal-Desktop-${pkgver}"

build() {
	export SOURCE_DATE_EPOCH=$(date +%s)

	Fcd
	Fpatchall

	Fexec git lfs install || Fdie
	Fexec sed 's#"node": "#&>=#' -i package.json || Fdie

	Fexec pnpm --prefix ./sticker-creator/ install
	Fexec pnpm install || Fdie
	Fexec pnpm --prefix ./sticker-creator/ run build || Fdie
	Fexec pnpm run build || Fdie

	Fmkdir usr/lib
	Fmkdir usr/bin

  	Fcprel release/linux-unpacked usr/lib/${pkgname}
	Fln "/usr/lib/${pkgname}/${pkgname}" usr/bin/

	Finstall 644 usr/share/applications/$pkgname.desktop
	for i in 16 24 32 48 64 128 256 512 1024; do
		Finstallrel 644 "build/icons/png/${i}x${i}.png" \
		"usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
	done

	# WTF
	Frm usr/lib/signal-desktop/resources/app.asar.unpacked/node_modules/ffi-napi/prebuilds/{darwin-x64,linux-arm64,win32-ia32,win32-x64}
	Frm usr/lib/signal-desktop/resources/app.asar.unpacked/node_modules/ffi-napi/node_modules/ref-napi/prebuilds/{darwin-x64,linux-arm64,win32-ia32,win32-x64}
	Frm usr/lib/signal-desktop/resources/app.asar.unpacked/node_modules/ref-napi/prebuilds/{darwin-x64,linux-arm64,win32-ia32,win32-x64}
	Frm usr/lib/signal-desktop/resources/app.asar.unpacked/node_modules/@signalapp/signal-client/prebuilds/{darwin-x64,linux-arm64,win32-ia32,win32-x64}


}


# optimization OK
